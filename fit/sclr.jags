# Scaled logit model for JAGS
#
# Data variables expected:
#   status - 0's and 1's (NA allowed)
#   loghilb, loghiub - lower/upper bounds for log HI titres (NA not allowed)
#
# Arseniy Khvorov
# Created 2019-09-19
# Last edit 2019-09/19

model {
  
  # Likelihood
  for (i in 1:n) {

    # Outcome
    status[i] ~ dbern(p[i])
    
    # Censoring
    loghi_imputed[i] ~ dnorm(loghimean, 1 / loghisd^2) T(loghilb[i], loghiub[i])
    
    # Outcome probability model
    p[i] <- lambda / (1 + exp(beta_0 + beta_hi * loghi_imputed[i]))
  }
  
  # Priors
  loghimean ~ dnorm(2, 1 / 2^2)
  loghisd ~ dexp(0.1)
  
  lambda ~ dbeta(1, 1)

  beta_0 ~ dnorm(-15, 1 / 10^2)
  beta_hi ~ dnorm(5, 1 / 5^2)
}
